buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.4.2"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.41" apply false
    id "org.jetbrains.kotlin.android" version "1.3.41" apply false
    id "com.github.ben-manes.versions" version "0.21.0"
    id "com.jfrog.bintray" version "1.8.4" apply false
}

subprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    group = "com.sch.neon"
    version = "0.11.0"

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs = ["-Xnew-inference"]
        }
    }
}

ext.versions = [
    kotlin           : "1.3.41",
    rxjava           : "2.2.10",
    rxkotlin         : "2.3.0",
    rxjava2extensions: "0.2.1",
    timber           : "4.7.1",

    junit            : [
        jupiter : "5.5.0",
        platform: "1.5.0",
    ],

    assertj          : "3.12.2",
    rxjava2junit5    : "1.0.0",
]
